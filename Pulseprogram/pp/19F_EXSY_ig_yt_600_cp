; 19F_EXSY_ig_yt_600_cp
; 230906 Y.Toyama
; used to record 19F-19F EXSY experiment 
; pseudo 3D, 1H decoupling (use f2 channel)
; set vc lists for mixing delays.
; The delays executed are vc*d7 + p52 + d16 + 4u + p53 + d16 + 4u.

; Using triple pulse excitation scheme to reduce baseline distortion.
; Ref: Wang et al., JMR 2021 332 107067,  JBNMR 2020 74:753
; ns = 16n, ph1 needs to be cycled.
 
; 1H 180 pulse train was added during the mixing time to ensure that
; the cross relaxation (and possibly cross-correlated relaxation) between
; nearby 1H is refocused.


;Modified from noesyfbphpr
;avance-version (12/01/11)
;2D homonuclear correlation via dipolar coupling 
;dipolar coupling may be due to noe or chemical exchange.
;phase sensitive
;with presaturation during relaxation delay and mixing time
;with optional inversion of 13C and/or 15N during evolution and
;   decoupling during acquisition
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>

;Define phases

#define zero ph=0.0
#define one ph=90.0
#define two ph=180.0
#define three ph=270.0

;Define delays

"d11=30m"
"d12=20u"
"in0=inf1"
"p22=2*p21"

define delay dly_lk
"dly_lk = d17"

define delay tauH
"tauH = 0.5*d7"

"d0=in0/2-p1*4/3.1416" 
"acqt0=-p1*2/3.1416"   ; select 'DIGIMOD = baseopt' to execute

;Define counters

define list<loopcounter> ncycT1 = <$VCLIST>

"l2=0" ; assign pointer to vc list
"l5=0"
"l6=0"

1 ze
 
 if "dly_lk > 41m" {
   2u
   print "error: ly_lk too lone < 40 ms"
   goto HaltAcqu
  }

2 d11
3 d12 do:f2 
  4u pl1:f1 pl2:f2

  "l5 = ncycT1[l2]"

  /* Purge element */

  20u UNBLKGRAD           ; delay(20.0e-6) unblank gradients & lk_hold
  2u pl11:f1
  (2u cw zero):f1
  dly_lk                  ; turn on cw decoupling for a delay of dly_lk
  2u do:f1
  (2u cw one):f1
  "DELTA = dly_lk/2.0"
  DELTA                   ; turn on cw decoupling for a delay of dly_lk
  2u do:f1
  p50:gp0*0.5             ; gradient 0 (50%)
  d16
  2u pl1:f1
  (p1 zero):f1            ; F90x
  2u                     
  p50:gp0                 ; gradient 0
  d16                  
  (p1 one):f1             ; F90y
  2u                     
  p50:gp0*0.3               ; gradient 0 (30%)
  d16                  
  10u BLKGRAD             ; delay(10.0e-6) blank gradients


  /* Interscan delay */
  d1 

  20u UNBLKGRAD   ; dly 20u, unblank gradients and lock on hold
  (p21 zero):f2
  p51:gp1
  d16

  /* This is the real start */
  (center (p1 ph1 d0 p1 ph2):f1 (p22 zero):f2)
  4u
  p52:gp2
  d16 
 
  /* Relaxation period */
 
  "DELTA = tauH - 0.5*p22"

5 DELTA                ; delay(tauH)
  (p22 zero):f2        ; H180x
  DELTA                ; delay(tauH)
  lo to 5 times l5  

  p53:gp3
  d16 pl12:f2
  4u BLKGRAD  
 
  /* Triple pulse excitation */
  (p1 ph3)
  1u
  (p1 ph4)
  1u
  (p1 ph5)
  go=2 ph31 cpd2:f2
  d11 do:f2  mc #0 to 2 
  F2QF(calclc(l2,1))
  F1PH(calph(ph1, +90), caldel(d0, +in0))

HaltAcqu, 1m
exit



ph1=0 0 0 0 0 0 0 0 2 2 2 2 2 2 2 2
ph2=0
ph3=0 2 0 2 0 2 0 2 0 2 0 2 0 2 0 2 1 3 1 3 1 3 1 3 1 3 1 3 1 3 1 3
ph4=2 2 0 0 2 2 0 0 2 2 0 0 2 2 0 0 3 3 1 1 3 3 1 1 3 3 1 1 3 3 1 1
ph5=0 0 0 0 2 2 2 2 0 0 0 0 2 2 2 2 1 1 1 1 3 3 3 3 1 1 1 1 3 3 3 3

ph31=0 2 2 0 2 0 0 2 2 0 0 2 0 2 2 0 1 3 3 1 3 1 1 3 3 1 1 3 1 3 3 1

;pl1 : f1 channel - power level for pulse (default)
;pl2 : f2 channel - power level for pulse (default)
;pl11 :f1 channel - power level for scrambling , typically about 23dB less power than high power
;pl12: f2 channel - power level for CPD/BB decoupling
;p1 : f1 channel -  90 degree high power pulse
;p21: f2 channel -  90 degree high power pulse
;p22: f2 channel - 180 degree high power pulse
;p50: gradient pulse 50 [512 usec]
;p51: gradient pulse 51 [512 usec]
;p52: gradient pulse 52 [256 usec]
;p53: gradient pulse 53 [256 usec]
;d0 : incremented delay (2D)
;d1 : relaxation delay; 1-5 * T1
;d7 : spacing between 1H pulses applied during the mixing time, typically 5 ms
;d8 : mixing time
;d11: delay for disk I/O                             [30 msec]
;d12: delay for power switching                      [20 usec]
;d16 : gradient recovery delay, 200us
;d17 : delay for scrambling - set to 40 ms
;inf1: 1/SW = 2 * DW
;in0: 1/(1 * SW) = 2 * DW
;nd0: 1
;ns: 16 * n
;ds: 32
;l2 : internal loop counter ignore
;l5 : internal loop counter ignore
;l6 : internal loop counter ignore
;l7 : Maximum counter used in the vclist (must be set properly)
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEQ
;cpd2: decoupling according to sequence defined by cpdprg2
;pcpd2: f2 channel - 90 degree pulse for decoupling sequence
;zgoptns:

;Processing

;PHC0(F1): 90
;PHC1(F1): -180
;FCOR(F1): 1

;for z-only gradients:
;gpz0: 60%
;gpz1: 40%
;gpz2: 60%
;gpz3: 50%

;use gradient files:
;gpnam0: SMSQ10.32
;gpnam1: SMSQ10.32
;gpnam2: SMSQ10.32
;gpnam3: SMSQ10.32

;$Id: noesyfbphpr,v 1.2 2012/01/31 17:49:27 ber Exp $
